///////////////////////////////////////////////////////////////////
//
// Служебный модуль с реализацией работы команды gitlab
//
// Структура модуля реализована в соответствии с рекомендациями 
// oscript-app-template (C) EvilBeaver
//
///////////////////////////////////////////////////////////////////

#Использовать fs
#Использовать logos

Перем Лог;

Функция ПолучитьЛог()
	Если Лог = Неопределено Тогда
		Лог = Логирование.ПолучитьЛог(ПараметрыСистемы.ИмяЛогаСистемы());
	КонецЕсли;
	Возврат Лог;
КонецФункции

Процедура ЗарегистрироватьКоманду(Знач ИмяКоманды, Знач Парсер) Экспорт

	ОписаниеКоманды = Парсер.ОписаниеКоманды(ИмяКоманды, "     Создать релиз gitlab");
	Парсер.ДобавитьИменованныйПараметрКоманды(ОписаниеКоманды, "--branch", "Имя ветки для создания релиза, пример: --branch master");
	Парсер.ДобавитьКоманду(ОписаниеКоманды);

КонецПроцедуры // ЗарегистрироватьКоманду

// Выполняет логику команды
// 
// Параметры:
//   ПараметрыКоманды - Соответствие - Соответствие ключей командной строки и их значений
//   ДополнительныеПараметры - Соответствие - дополнительные параметры (необязательно)
//
// Возвращаемое значение:
//   Число - результат выполнения команды
//
Функция ВыполнитьКоманду(Знач ПараметрыКоманды, Знач ДополнительныеПараметры) Экспорт

	Лог = ПолучитьЛог();
	Лог.Отладка("Команда gitlab");

	Для каждого Параметр Из ПараметрыКоманды Цикл
		Лог.Отладка(Параметр.Ключ + " " + Параметр.значение);
	КонецЦикла;

	АдресСервера = ПараметрыКоманды["--server"];
	Токен = ПараметрыКоманды["--token"];
	Проект = ПараметрыКоманды["--project-id"];
	Ветка = ПараметрыКоманды["--branch"];

	ГитЛаб = Новый gitlab(АдресСервера, Токен);

	КаталогДляСохранения = ОбъединитьПути(КаталогВременныхФайлов(), "release");
	ФС.ОбеспечитьПустойКаталог(КаталогДляСохранения);

	ПоследнийРелиз = ГитЛаб.ПолучитьНомерПоследнегоРелиза(Проект);
	ИмяРелиза = НовоеИмяРелиза(ПоследнийРелиз);
	Лог.Отладка(ИмяРелиза);

	ГитЛаб.СоздатьРелиз(Проект, ИмяРелиза, Ветка);

	Лог.Информация("Успешно создан релиз " + ИмяРелиза);

	Возврат МенеджерКомандПриложения.РезультатыКоманд().Успех;

КонецФункции // ВыполнитьКоманду

Функция НовоеИмяРелиза(Знач ПоследнийРелиз)
	
	Лог.Отладка(ПоследнийРелиз);
	Массив = СтрРазделить(ПоследнийРелиз, ".");
	ПоследняяСтрока = Массив[Массив.ВГраница()];
	НовыйНомер = Число(ПоследняяСтрока) + 1;
	Массив.Удалить(Массив.ВГраница());
	Массив.Добавить(НовыйНомер);
	Возврат СтрСоединить(Массив, ".");
	
КонецФункции
